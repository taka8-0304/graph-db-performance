pluginManagement {
	plugins {
		id 'com.github.spotbugs' version '4.0.0'
	}
	repositories {
		mavenLocal()
		mavenCentral()
		gradlePluginPortal()
	}
}
dependencyResolutionManagement {
	repositories {
		mavenLocal()
		mavenCentral()
		gradlePluginPortal()
	}
}

includeBuild('./gradle-plugin')

rootProject.name = 'graph-db-performance'

def targets = [
	'module': '',
	'deploy': 'deploy',
	'database': ''
]

def addProject(projectName, targetDir) {
	if (new File(targetDir, 'build.gradle').exists()) {
		include projectName
		def p = project(':' + projectName)
		p.projectDir = targetDir
	} else {
		targetDir.listFiles().each {
			addProject(projectName + '-' + it.name, it)
		}
	}
}

targets.each {
	def projectName = it.value == ''?rootProject.name:rootProject.name + '-' + it.value
	def targetDir = new File(rootProject.projectDir, it.key)
	addProject(projectName, targetDir)
}
